CD
CD*** SUBROUTINE LDRLUF ***********************************************
CD---------------------------------------------------------------------
CD    Назначение:
CD      LU-разложение вещественной матрицы.
CD---------------------------------------------------------------------
CD    Примечание:
CD       расположение массива матрицы по строкам для вызова из языка C
CD---------------------------------------------------------------------
CD    Вызов:
CD      CALL LDRLUF(A, N, IA, IPVT)
CD---------------------------------------------------------------------
CD    Формальные параметры:
CD
CD      Имя  Размер Тип Тип_В/В   Назначение
CD
CD      A    IA*N     D     В/В    массив, при вызове содержащий
CD                                матрицу A(N,N); при возврате
CD                                содержит факторизованную матрицу:
CD                                верхнюю треугольную матрицу U и,
CD                                учитывая перестановки, нижнюю
CD                                треугольную матрицу L;
CD      N       -     I     Вх     порядок матрицы A;
CD      IA      -     I     Вх     первая размерность массива A;
CD      IPVT    N     I     Вых    вектор, содержащий информацию о
CD                                выборе ведущих элементов:
CD                                 IPVT(K) - индекс K-й ведущей строки,
CD                                 IPVT(N) =(-1)**(число перестановок);
CD---------------------------------------------------------------------
CD    Требуемые подпрограммы и функции:
CD      стандартные: DABS
CD      внешние    : нет
CD--------------------------------------------------------------------
CD    Связь с внешними модулями:
CD      может использоваться самостоятельно или совместно с LDRLUS
CD--------------------------------------------------------------------
CD    Метод: 
CD      Форсайт Дж., Малькольм М., Моулер К. Машинные методы 
CD    математических вычислений / М. изд.: Мир, 1980. - 280 c.
CD--------------------------------------------------------------------
CD    Внешняя среда: Электроника-79,   RSX-11M,   FORTRAN IV V02.2-1
CD--------------------------------------------------------------------
CD    Организация: МЭИ, кафедра РПДУ   Демидов В.М.         18.05.89
CD                                     Медунов А.Л.         21.02.2015
CD--------------------------------------------------------------------
      SUBROUTINE LDRLUF(A, N, IA, IPVT)
C----------------------------------------------------------------------
C--   LU-разложение вещественной матрицы и оценка её обусловленности
C----------------------------------------------------------------------
      IMPLICIT NONE
C--
      DOUBLE PRECISION A(IA,N)
      INTEGER          N
      INTEGER          IA
      INTEGER          IPVT(N)
C--
      DOUBLE PRECISION T, ZERO, DABS
      INTEGER          NM1, I, J, K, KP1, KB, KM1, M
C--
      PARAMETER (ZERO = 0.0D00)
C--
C--   аббревиатура имени модуля: LU-ФАКТОРИЗАЦИЯ
C--
      IPVT(1) = 1
      IF (N.EQ.1) GOTO 3000
      NM1 = N - 1
C--
C--   LU-разложение ---------------------------------------------------
C--
      DO 35 K=1,NM1
         KP1 = K + 1
C--      поиск ведущего элемента
         M = K
         DO 15 I=KP1,N
            IF (DABS(A(K,I)).GT.DABS(A(K,M))) M = I
   15    CONTINUE
         IPVT(K) = M
         IF (M.NE.K) IPVT(N) = -IPVT(N)
         T = A(K,M)
         A(K,M) = A(K,K)
         A(K,K) = T 
C--      пропуск, если ведущий элемент равен нулю
         IF (T.EQ.ZERO) GOTO 35
C--      вычисление множителей
         DO 20 I=KP1,N
            A(K,I) = -A(K,I) / T
   20    CONTINUE
C--      перестановка и исключение по столбцам
         DO 30 J=KP1,N
            T = A(J,M)
            A(J,M) = A(J,K)
            A(J,K) = T
            IF (T.EQ.ZERO) GOTO 30
            DO 25 I=KP1,N
               A(J,I) = A(J,I) + A(K,I)*T
   25       CONTINUE
   30    CONTINUE
   35 CONTINUE
C
 3000 RETURN
      END
